model ascest
uses "mmxprs"; !gain access to the Xpress-Optimizer solver
uses "mmsystem" !To measure time

forward procedure solucion_aleatoria
	
declarations
	n,m:integer
	zopt:real
	tiempo_max = 1
	!archivo = "mknap1_2.dat"
	archivo = "mknapcb1_4.dat"
	!archivo = "mknapcb1_1.dat"
end-declarations

fopen(archivo,F_INPUT)
readln(n,m,zopt)

declarations
	objetos=1..n
	recursos=1..m
	p:array(objetos) of real
	a:array(recursos,objetos) of integer
	b:array(recursos) of integer
	x:array(objetos) of mpvar
	iter_max=2000 !par�metro mejora estoc�sctica
	jp,kp:integer
	ind0,ind1:array(objetos) of integer
end-declarations

forall(j in objetos) read(p(j))
forall(i in recursos,j in objetos) read(a(i,j))
forall(i in recursos) read(b(i))
fclose(F_INPUT)

writeln("Problema MKP, datos: ",archivo)
writeln("n= ",n,", m= ",m)
writeln("Valor de libreria: ",zopt)

obj:= sum(j in objetos)p(j)*x(j)
!Restricciones
forall(i in recursos) rec(i):=
	sum(j in objetos)a(i,j)*x(j)<=b(i)
	
forall(j in objetos)x(j) is_binary

!fijamos el tiempo maximo de ejecucion en segundos
t1:=gettime
setparam("XPRS_MAXTIME",tiempo_max)
maximize(obj)

if(getprobstat= XPRS_OPT) then
	writeln("Solucion optima en ",gettime-t1," segundos")
	writeln("Beneficio = ",getobjval)
elif(getprobstat= XPRS_UNF) then
	writeln("Solucion obtenida en ",tiempo_max," segundos")
	writeln("Beneficio = ",getobjval)
elif(getprobstat= XPRS_INF) then
	writeln("Problema no factible")
end-if
writeln("Tiempo= ",gettime-t1)


!exportprob(EP_MAX,"",obj)


!===============Metodo greedy================================
t1:=gettime
declarations
	xsol:array(objetos)of integer
	u:array(recursos)of integer
	g:array(objetos)of real
	criterio_greedy =4	! =1 cociente p(j)/sum a(i,j)
						! =2 p(j)/sum a(i,j)/b(i)
						! =3 x(j).sol
						! =4 p(j)/sum a(i,j)*ps(i)
	zheur:real
end-declarations
maximize(XPRS_LIN,obj) ! resolver la relajaci�n lineal para el caso 3
forall(j in objetos)do
	if(criterio_greedy=1)then
		g(j):=p(j)/sum(i in recursos)a(i,j)
	elif(criterio_greedy=2)then
		g(j):=p(j)/sum(i in recursos)(a(i,j)/b(i))
	elif(criterio_greedy=3)then
		g(j):=x(j).sol
	elif(criterio_greedy=4)then
		g(j):=p(j)/sum(i in recursos)a(i,j)*rec(i).dual
	end-if
end-do

! Inicializaciones:
zheur:=0

final:=0
while(final=0)do
	! encontrar la lista de candidatos
	nc:=0
	forall(j in objetos|xsol(j)=0)do
		candidato(j):=0
		nr:=0
		forall(i in recursos)do 
			if(u(i)+a(i,j)<=b(i))then
				nr:=nr+1
			end-if
		end-do
		if(nr=m) then 
			candidato(j):=1
			nc:=nc+1
		end-if
	end-do
	if(nc=0)then
		final:=1
	else
		! encontrar el candidato que maximiza g(j)
		gmax:=-99.
		forall(j in objetos|xsol(j)=0 and candidato(j)=1)do
			if(g(j)>gmax)then
				gmax:=g(j)
				jmax:=j
			end-if
		end-do
	
		! seleccionamos el objeto jmax
		xsol(jmax):=1
		zheur:=zheur+p(jmax)
		forall(i in recursos)do
			u(i):=u(i)+a(i,jmax)
		end-do
	end-if
	! terminacion
end-do

writeln("Solucion greedy ",criterio_greedy,", z= ",zheur)
writeln("Tiempo= ",gettime-t1," segundos")


!===============Metaheurestica: Ascenso estocastico=======================
zmejor:=zheur
iter:=0
final:=0
nfracasos:=0
t2:=gettime

while(final=0)do
	!elegimos solucion aleatoria en entorno
	solucion_aleatoria
	zheur1:=zheur+p(jp)-p(kp)
	if(zheur1>zheur)then !se hace el cambio
		xsol(jp):=1
		xsol(kp):=0
		zheur:=zheur1
		forall(i in recursos)u(i):=u(i)+a(i,jp)-a(i,kp)
		if(zheur1>zmejor)then 
			zmejor:=zheur1
			nfracasos:=0
		end-if
	else
		nfracasos:=nfracasos+1	
	end-if
	if(nfracasos=iter_max)then
		final:=1
	end-if
end-do
writeln("Solucion ascenso estocastico z= ",zmejor)
writeln("Tiempo= ",gettime-t2," segundos")

!============

procedure solucion_aleatoria
	fin:=0
	while(fin=0)do
		n0:=0
		n1:=0
		forall(j in objetos)do
			if(xsol(j)=0)then
				n0:=n0+1
				ind0(n0):=j
			elif(xsol(j)=1)then
				n1:=n1+1
				ind1(n1):=j
			end-if
		end-do
		jp:=ind0(ceil(n0*random)) !jp indice aleatorio con valor 0
		kp:=ind1(ceil(n1*random)) !kp �ndice aleatorio con valor 1
		!comprobar si el intercambio es factible
		nr:=0
		forall(i in recursos)do
			if(u(i)+a(i,jp)-a(i,kp)<=b(i))then
				nr:=nr+1
			end-if
		end-do
		if(nr=m)then
			fin:=1
		end-if
	end-do
end-procedure

end-model